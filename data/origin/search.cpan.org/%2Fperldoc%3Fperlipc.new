<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
 <head>
  <link rel="stylesheet" href="http://st.pimg.net/tucs/style.css" type="text/css" />
<style>
.styleswitch {
  text-align: right;
}
</style>


<script type="text/javascript" src="http://ajax.googleapis.com/ajax/libs/jquery/1.4.2/jquery.min.js"></script>
<script type="text/javascript" src="http://st.pimg.net/tucs/js/jquery.cookie.js"></script>
<script type="text/javascript" src="http://st.pimg.net/tucs/js/sh_main.min.js"></script>
<script type="text/javascript" src="http://st.pimg.net/tucs/js/sh_perl.min.js"></script>
<script type="text/javascript" src="http://st.pimg.net/tucs/js/jquery.styleswitch.js"></script>

<link rel="stylesheet" href="http://st.pimg.net/tucs/print.css" type="text/css" media="print" />
  <link rel="alternate" type="application/rss+xml" title="RSS 1.0" href="http://search.cpan.org/uploads.rdf" />
  <link rel="search" href="http://st.pimg.net/tucs/opensearch.xml" type="application/opensearchdescription+xml" title="SearchCPAN" />
  <title>perlipc - search.cpan.org</title>
 <script type="text/javascript">
    var _gaq = _gaq || [];
    _gaq.push(['_setAccount', 'UA-3528438-1']);
    _gaq.push(["_setCustomVar",2,"Distribution","perl",3]);
    _gaq.push(["_setCustomVar",5,"Release","perl-5.14.2",3]);
    _gaq.push(["_setCustomVar",4,"Document","perlipc",3]);
    _gaq.push(["_setCustomVar",1,"Author","FLORA",3]);
    _gaq.push(['_trackPageview']);
  </script>
 </head>
 <body id="cpansearch">
<center><div class="logo"><a href="/"><img src="http://st.pimg.net/tucs/img/cpan_banner.png" alt="CPAN"></a></div></center>
<div class="menubar">
 <a href="/">Home</a>
&middot; <a href="/author/">Authors</a>
&middot; <a href="/recent">Recent</a>
&middot; <a href="http://log.perl.org/cpansearch/">News</a>
&middot; <a href="/mirror">Mirrors</a>
&middot; <a href="/faq.html">FAQ</a>
&middot; <a href="/feedback">Feedback</a>
</div>
<form method="get" action="/search" name="f" class="searchbox">
<input type="text" name="query" value="" size="35">
<br>in <select name="mode">
 <option value="all">All</option>
 <option value="module" >Modules</option>
 <option value="dist" >Distributions</option>
 <option value="author" >Authors</option>
</select>&nbsp;<input type="submit" value="CPAN Search">
</form>


 <a name="_top"></a>
  <div class=path>
<div id=permalink class="noprint"><a href="/perldoc?perlipc">permalink</a></div>
  <a href="/~flora/">Florian Ragwitz</a> &gt;
  <a href="/~flora/perl-5.14.2/">perl-5.14.2</a> &gt;
  perlipc
 </div>

<div class="noprint" style="float:right;align:left;width:19ex">
<a href="http://hexten.net/cpan-faces/"><img src="http://www.gravatar.com/avatar/fd2bdaaefe41908133793d1ddf38f75d?r=g&s=80&d=http%3A%2F%2Fst.pimg.net%2Ftucs%2Fimg%2Fwho.png" width=80 height=80 
style="float:right"
/></a>
<br style="clear:both"/>
<p style="text-align:right">Download:<br/> <a href="/CPAN/authors/id/F/FL/FLORA/perl-5.14.2.tar.gz">perl-5.14.2.tar.gz</a></p>
<p style="text-align:right"><a href="http://www.annocpan.org/~FLORA/perl-5.14.2/pod/perlipc.pod">Annotate this POD
(1)
</a></p>
     <p style="text-align:right"><a href="http://www.perl.org/">Website</a></p>

<div style="float:right">

</div>
</div>
<span class="noprint">
  <a href="/src/FLORA/perl-5.14.2/pod/perlipc.pod">Source</a> &nbsp;
  Latest&nbsp;Release:&nbsp;<a href="/~abigail/perl/pod/perlipc.pod">perl-5.15.9</a>
</span>
<a name="___top"></a>
<div class=pod>
<div class=toc>
<div class='indexgroup'>
<ul   class='indexList indexList1'>
  <li class='indexItem indexItem1'><a href='#NAME'>NAME</a>
  <li class='indexItem indexItem1'><a href='#DESCRIPTION'>DESCRIPTION</a>
  <li class='indexItem indexItem1'><a href='#Signals'>Signals</a>
  <ul   class='indexList indexList2'>
    <li class='indexItem indexItem2'><a href='#Handling_the_SIGHUP_Signal_in_Daemons'>Handling the SIGHUP Signal in Daemons</a>
    <li class='indexItem indexItem2'><a href='#Deferred_Signals_(Safe_Signals)'>Deferred Signals (Safe Signals)</a>
  </ul>
  <li class='indexItem indexItem1'><a href='#Named_Pipes'>Named Pipes</a>
  <li class='indexItem indexItem1'><a href='#Using_open()_for_IPC'>Using open() for IPC</a>
  <ul   class='indexList indexList2'>
    <li class='indexItem indexItem2'><a href='#Filehandles'>Filehandles</a>
    <li class='indexItem indexItem2'><a href='#Background_Processes'>Background Processes</a>
    <li class='indexItem indexItem2'><a href='#Complete_Dissociation_of_Child_from_Parent'>Complete Dissociation of Child from Parent</a>
    <li class='indexItem indexItem2'><a href='#Safe_Pipe_Opens'>Safe Pipe Opens</a>
    <li class='indexItem indexItem2'><a href='#Avoiding_Pipe_Deadlocks'>Avoiding Pipe Deadlocks</a>
    <li class='indexItem indexItem2'><a href='#Bidirectional_Communication_with_Another_Process'>Bidirectional Communication with Another Process</a>
    <li class='indexItem indexItem2'><a href='#Bidirectional_Communication_with_Yourself'>Bidirectional Communication with Yourself</a>
  </ul>
  <li class='indexItem indexItem1'><a href='#Sockets:_Client/Server_Communication'>Sockets: Client/Server Communication</a>
  <ul   class='indexList indexList2'>
    <li class='indexItem indexItem2'><a href='#Internet_Line_Terminators'>Internet Line Terminators</a>
    <li class='indexItem indexItem2'><a href='#Internet_TCP_Clients_and_Servers'>Internet TCP Clients and Servers</a>
    <li class='indexItem indexItem2'><a href='#Unix-Domain_TCP_Clients_and_Servers'>Unix-Domain TCP Clients and Servers</a>
  </ul>
  <li class='indexItem indexItem1'><a href='#TCP_Clients_with_IO::Socket'>TCP Clients with IO::Socket</a>
  <ul   class='indexList indexList2'>
    <li class='indexItem indexItem2'><a href='#A_Simple_Client'>A Simple Client</a>
    <li class='indexItem indexItem2'><a href='#A_Webget_Client'>A Webget Client</a>
    <li class='indexItem indexItem2'><a href='#Interactive_Client_with_IO::Socket'>Interactive Client with IO::Socket</a>
  </ul>
  <li class='indexItem indexItem1'><a href='#TCP_Servers_with_IO::Socket'>TCP Servers with IO::Socket</a>
  <li class='indexItem indexItem1'><a href='#UDP:_Message_Passing'>UDP: Message Passing</a>
  <li class='indexItem indexItem1'><a href='#SysV_IPC'>SysV IPC</a>
  <li class='indexItem indexItem1'><a href='#NOTES'>NOTES</a>
  <li class='indexItem indexItem1'><a href='#BUGS'>BUGS</a>
  <li class='indexItem indexItem1'><a href='#AUTHOR'>AUTHOR</a>
  <li class='indexItem indexItem1'><a href='#SEE_ALSO'>SEE ALSO</a>
</ul>
</div>
</div>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="NAME"
>NAME <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>perlipc - Perl interprocess communication (signals,
fifos,
pipes,
safe subprocesses,
sockets,
and semaphores)</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="DESCRIPTION"
>DESCRIPTION <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>The basic IPC facilities of Perl are built out of the good old Unix signals,
named pipes,
pipe opens,
the Berkeley socket routines,
and SysV IPC calls.
Each is used in slightly different situations.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="Signals"
>Signals <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>Perl uses a simple signal handling model: the %SIG hash contains names or references of user-installed signal handlers.
These handlers will be called with an argument which is the name of the signal that triggered it.
A signal may be generated intentionally from a particular keyboard sequence like control-C or control-Z,
sent to you from another process,
or triggered automatically by the kernel when special events transpire,
like a child process exiting,
your own process running out of stack space,
or hitting a process file-size limit.</p>

<p>For example,
to trap an interrupt signal,
set up a handler like this:</p>

<pre class="sh_perl">    our $shucks;

    sub catch_zap {
        my $signame = shift;
        $shucks++;
        die &#34;Somebody sent me a SIG$signame&#34;;
    }
    $SIG{INT} = __PACKAGE__ . &#34;::catch_zap&#34;;  
    $SIG{INT} = \&#38;catch_zap;  # best strategy</pre>

<p>Prior to Perl 5.7.3 it was necessary to do as little as you possibly could in your handler; notice how all we do is set a global variable and then raise an exception. That&#39;s because on most systems, libraries are not re-entrant; particularly, memory allocation and I/O routines are not. That meant that doing nearly <i>anything</i> in your handler could in theory trigger a memory fault and subsequent core dump - see <a href="#Deferred_Signals_(Safe_Signals)" class="podlinkpod"
>&#34;Deferred Signals (Safe Signals)&#34;</a> below.</p>

<p>The names of the signals are the ones listed out by <code>kill -l</code> on your system, or you can retrieve them from the Config module. Set up an @signame list indexed by number to get the name and a %signo hash table indexed by name to get the number:</p>

<pre class="sh_perl">    use Config;
    defined($Config{sig_name})  || die &#34;No sigs?&#34;;
    foreach $name (split(&#34; &#34;, $Config{sig_name})) {
        $signo{$name} = $i;
        $signame[$i]  = $name;
        $i++;
    }</pre>

<p>So to check whether signal 17 and SIGALRM were the same, do just this:</p>

<pre class="sh_perl">    print &#34;signal #17 = $signame[17]\n&#34;;
    if ($signo{ALRM}) {
        print &#34;SIGALRM is $signo{ALRM}\n&#34;;
    }</pre>

<p>You may also choose to assign the strings <code>&#34;IGNORE&#34;</code> or <code>&#34;DEFAULT&#34;</code> as the handler, in which case Perl will try to discard the signal or do the default thing.</p>

<p>On most Unix platforms, the <code>CHLD</code> (sometimes also known as <code>CLD</code>) signal has special behavior with respect to a value of <code>&#34;IGNORE&#34;</code>. Setting <code>$SIG{CHLD}</code> to <code>&#34;IGNORE&#34;</code> on such a platform has the effect of not creating zombie processes when the parent process fails to <code>wait()</code> on its child processes (i.e., child processes are automatically reaped). Calling <code>wait()</code> with <code>$SIG{CHLD}</code> set to <code>&#34;IGNORE&#34;</code> usually returns <code>-1</code> on such platforms.</p>

<p>Some signals can be neither trapped nor ignored, such as the KILL and STOP (but not the TSTP) signals. One strategy for temporarily ignoring signals is to use a local() on that hash element, automatically restoring a previous value once your block is exited. Remember that values created by the dynamically-scoped local() are &#34;inherited&#34; by functions called from within their caller&#39;s scope.</p>

<pre class="sh_perl">    sub precious {
        local $SIG{INT} = &#34;IGNORE&#34;;
        more_functions();
    }
    sub more_functions {
        # interrupts still ignored, for now...
    }</pre>

<p>Sending a signal to a negative process ID means that you send the signal to the entire Unix process group. This code sends a hang-up signal to all processes in the current process group, and also sets $SIG{HUP} to <code>&#34;IGNORE&#34;</code> so it doesn&#39;t kill itself:</p>

<pre class="sh_perl">    # block scope for local
    {
        local $SIG{HUP} = &#34;IGNORE&#34;;
        kill HUP =&#62; -$$;
        # snazzy writing of: kill(&#34;HUP&#34;, -$$)
    }</pre>

<p>Another interesting signal to send is signal number zero. This doesn&#39;t actually affect a child process, but instead checks whether it&#39;s alive or has changed its UID.</p>

<pre class="sh_perl">    unless (kill 0 =&#62; $kid_pid) {
        warn &#34;something wicked happened to $kid_pid&#34;;
    }</pre>

<p>When directed at a process whose UID is not identical to that of the sending process, signal number zero may fail because you lack permission to send the signal, even though the process is alive. You may be able to determine the cause of failure using <code>%!</code>.</p>

<pre class="sh_perl">    unless (kill(0 =&#62; $pid) || $!{EPERM}) {
        warn &#34;$pid looks dead&#34;;
    }</pre>

<p>You might also want to employ anonymous functions for simple signal handlers:</p>

<pre class="sh_perl">    $SIG{INT} = sub { die &#34;\nOutta here!\n&#34; };</pre>

<p>But that will be problematic for the more complicated handlers that need to reinstall themselves. Because Perl&#39;s signal mechanism is currently based on the signal(3) function from the C library, you may sometimes be so unfortunate as to run on systems where that function is &#34;broken&#34;; that is, it behaves in the old unreliable SysV way rather than the newer, more reasonable BSD and POSIX fashion. So you&#39;ll see defensive people writing signal handlers like this:</p>

<pre class="sh_perl">    sub REAPER {
        $waitedpid = wait;
        # loathe SysV: it makes us not only reinstate
        # the handler, but place it after the wait
        $SIG{CHLD} = \&#38;REAPER;
    }
    $SIG{CHLD} = \&#38;REAPER;
    # now do something that forks...</pre>

<p>or better still:</p>

<pre class="sh_perl">    use POSIX &#34;:sys_wait_h&#34;;
    sub REAPER {
        my $child;
        # If a second child dies while in the signal handler caused by the
        # first death, we won&#39;t get another signal. So must loop here else
        # we will leave the unreaped child as a zombie. And the next time
        # two children die we get another zombie. And so on.
        while (($child = waitpid(-1, WNOHANG)) &#62; 0) {
            $Kid_Status{$child} = $?;
        }
        $SIG{CHLD} = \&#38;REAPER;  # still loathe SysV
    }
    $SIG{CHLD} = \&#38;REAPER;
    # do something that forks...</pre>

<p>Be careful: qx(), system(), and some modules for calling external commands do a fork(), then wait() for the result. Thus, your signal handler (<code>&#38;REAPER</code> in the example) will be called. Because wait() was already called by system() or qx(), the wait() in the signal handler will see no more zombies and will therefore block.</p>

<p>The best way to prevent this issue is to use waitpid(), as in the following example:</p>

<pre class="sh_perl">    use POSIX &#34;:sys_wait_h&#34;; # for nonblocking read

    my %children;

    $SIG{CHLD} = sub {
        # don&#39;t change $! and $? outside handler
        local ($!, $?);
        my $pid = waitpid(-1, WNOHANG);
        return if $pid == -1;
        return unless defined $children{$pid};
        delete $children{$pid};
        cleanup_child($pid, $?);
    };

    while (1) {
        my $pid = fork();
        die &#34;cannot fork&#34; unless defined $pid;
        if ($pid == 0) {
            # ...
            exit 0;
        } else {
            $children{$pid}=1;
            # ...
            system($command);
            # ...
       }
    }</pre>

<p>Signal handling is also used for timeouts in Unix. While safely protected within an <code>eval{}</code> block, you set a signal handler to trap alarm signals and then schedule to have one delivered to you in some number of seconds. Then try your blocking operation, clearing the alarm when it&#39;s done but not before you&#39;ve exited your <code>eval{}</code> block. If it goes off, you&#39;ll use die() to jump out of the block, much as you might using longjmp() or throw() in other languages.</p>

<p>Here&#39;s an example:</p>

<pre class="sh_perl">    my $ALARM_EXCEPTION = &#34;alarm clock restart&#34;;
    eval {
        local $SIG{ALRM} = sub { die $ALARM_EXCEPTION };
        alarm 10;
        flock(FH, 2)    # blocking write lock
                        || die &#34;cannot flock: $!&#34;;
        alarm 0;
    };
    if ($@ &#38;&#38; $@ !~ quotemeta($ALARM_EXCEPTION)) { die }</pre>

<p>If the operation being timed out is system() or qx(), this technique is liable to generate zombies. If this matters to you, you&#39;ll need to do your own fork() and exec(), and kill the errant child process.</p>

<p>For more complex signal handling, you might see the standard POSIX module. Lamentably, this is almost entirely undocumented, but the <em>t/lib/posix.t</em> file from the Perl source distribution has some examples in it.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Handling_the_SIGHUP_Signal_in_Daemons"
>Handling the SIGHUP Signal in Daemons</a></h2>

<p>A process that usually starts when the system boots and shuts down when the system is shut down is called a daemon (Disk And Execution MONitor). If a daemon process has a configuration file which is modified after the process has been started, there should be a way to tell that process to reread its configuration file without stopping the process. Many daemons provide this mechanism using a <code>SIGHUP</code> signal handler. When you want to tell the daemon to reread the file, simply send it the <code>SIGHUP</code> signal.</p>

<p>Not all platforms automatically reinstall their (native) signal handlers after a signal delivery. This means that the handler works the first time the signal is sent, only. The solution to this problem is to use <code>POSIX</code> signal handlers if available; their behavior is well-defined.</p>

<p>The following example implements a simple daemon, which restarts itself every time the <code>SIGHUP</code> signal is received. The actual code is located in the subroutine <code>code()</code>, which just prints some debugging info to show that it works; it should be replaced with the real code.</p>

<pre class="sh_perl">  #!/usr/bin/perl -w

  use POSIX ();
  use FindBin ();
  use File::Basename ();
  use File::Spec::Functions;

  $| = 1;

  # make the daemon cross-platform, so exec always calls the script
  # itself with the right path, no matter how the script was invoked.
  my $script = File::Basename::basename($0);
  my $SELF  = catfile($FindBin::Bin, $script);

  # POSIX unmasks the sigprocmask properly
  my $sigset = POSIX::SigSet-&#62;new();
  my $action = POSIX::SigAction-&#62;new(&#34;sigHUP_handler&#34;,
                                     $sigset,
                                     &#38;POSIX::SA_NODEFER);
  POSIX::sigaction(&#38;POSIX::SIGHUP, $action);

  sub sigHUP_handler {
      print &#34;got SIGHUP\n&#34;;
      exec($SELF, @ARGV)        || die &#34;$0: couldn&#39;t restart: $!&#34;;
  }

  code();

  sub code {
      print &#34;PID: $$\n&#34;;
      print &#34;ARGV: @ARGV\n&#34;;
      my $count = 0;
      while (++$count) {
          sleep 2;
          print &#34;$count\n&#34;;
      }
  }</pre>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Deferred_Signals_(Safe_Signals)"
>Deferred Signals (Safe Signals)</a></h2>

<p>Before Perl 5.7.3, installing Perl code to deal with signals exposed you to danger from two things. First, few system library functions are re-entrant. If the signal interrupts while Perl is executing one function (like malloc(3) or printf(3)), and your signal handler then calls the same function again, you could get unpredictable behavior--often, a core dump. Second, Perl isn&#39;t itself re-entrant at the lowest levels. If the signal interrupts Perl while Perl is changing its own internal data structures, similarly unpredictable behavior may result.</p>

<p>There were two things you could do, knowing this: be paranoid or be pragmatic. The paranoid approach was to do as little as possible in your signal handler. Set an existing integer variable that already has a value, and return. This doesn&#39;t help you if you&#39;re in a slow system call, which will just restart. That means you have to <code>die</code> to longjmp(3) out of the handler. Even this is a little cavalier for the true paranoiac, who avoids <code>die</code> in a handler because the system <i>is</i> out to get you. The pragmatic approach was to say &#34;I know the risks, but prefer the convenience&#34;, and to do anything you wanted in your signal handler, and be prepared to clean up core dumps now and again.</p>

<p>Perl 5.7.3 and later avoid these problems by &#34;deferring&#34; signals. That is, when the signal is delivered to the process by the system (to the C code that implements Perl) a flag is set, and the handler returns immediately. Then at strategic &#34;safe&#34; points in the Perl interpreter (e.g. when it is about to execute a new opcode) the flags are checked and the Perl level handler from %SIG is executed. The &#34;deferred&#34; scheme allows much more flexibility in the coding of signal handlers as we know the Perl interpreter is in a safe state, and that we are not in a system library function when the handler is called. However the implementation does differ from previous Perls in the following ways:</p>

<dl>
<dt><a name='Long'></a><a name="Long-running_opcodes"
>Long-running opcodes</a></dt>

<dd>
<p>As the Perl interpreter looks at signal flags only when it is about to execute a new opcode, a signal that arrives during a long-running opcode (e.g. a regular expression operation on a very large string) will not be seen until the current opcode completes.</p>

<p>If a signal of any given type fires multiple times during an opcode (such as from a fine-grained timer), the handler for that signal will be called only once, after the opcode completes; all other instances will be discarded. Furthermore, if your system&#39;s signal queue gets flooded to the point that there are signals that have been raised but not yet caught (and thus not deferred) at the time an opcode completes, those signals may well be caught and deferred during subsequent opcodes, with sometimes surprising results. For example, you may see alarms delivered even after calling <code>alarm(0)</code> as the latter stops the raising of alarms but does not cancel the delivery of alarms raised but not yet caught. Do not depend on the behaviors described in this paragraph as they are side effects of the current implementation and may change in future versions of Perl.</p>

<dt><a name='Interrupting'></a><a name="Interrupting_IO"
>Interrupting IO</a></dt>

<dd>
<p>When a signal is delivered (e.g., SIGINT from a control-C) the operating system breaks into IO operations like <i>read</i>(2), which is used to implement Perl&#39;s readline() function, the <code>&#60;&#62;</code> operator. On older Perls the handler was called immediately (and as <code>read</code> is not &#34;unsafe&#34;, this worked well). With the &#34;deferred&#34; scheme the handler is <i>not</i> called immediately, and if Perl is using the system&#39;s <code>stdio</code> library that library may restart the <code>read</code> without returning to Perl to give it a chance to call the %SIG handler. If this happens on your system the solution is to use the <code>:perlio</code> layer to do IO--at least on those handles that you want to be able to break into with signals. (The <code>:perlio</code> layer checks the signal flags and calls %SIG handlers before resuming IO operation.)</p>

<p>The default in Perl 5.7.3 and later is to automatically use the <code>:perlio</code> layer.</p>

<p>Note that it is not advisable to access a file handle within a signal handler where that signal has interrupted an I/O operation on that same handle. While perl will at least try hard not to crash, there are no guarantees of data integrity; for example, some data might get dropped or written twice.</p>

<p>Some networking library functions like gethostbyname() are known to have their own implementations of timeouts which may conflict with your timeouts. If you have problems with such functions, try using the POSIX sigaction() function, which bypasses Perl safe signals. Be warned that this does subject you to possible memory corruption, as described above.</p>

<p>Instead of setting <code>$SIG{ALRM}</code>:</p>

<pre class="sh_perl">   local $SIG{ALRM} = sub { die &#34;alarm&#34; };</pre>

<p>try something like the following:</p>

<pre class="sh_perl">    use POSIX qw(SIGALRM);
    POSIX::sigaction(SIGALRM,
                     POSIX::SigAction-&#62;new(sub { die &#34;alarm&#34; }))
            || die &#34;Error setting SIGALRM handler: $!\n&#34;;</pre>

<p>Another way to disable the safe signal behavior locally is to use the <code>Perl::Unsafe::Signals</code> module from CPAN, which affects all signals.</p>

<dt><a name='Restartable'></a><a name="Restartable_system_calls"
>Restartable system calls</a></dt>

<dd>
<p>On systems that supported it, older versions of Perl used the SA_RESTART flag when installing %SIG handlers. This meant that restartable system calls would continue rather than returning when a signal arrived. In order to deliver deferred signals promptly, Perl 5.7.3 and later do <i>not</i> use SA_RESTART. Consequently, restartable system calls can fail (with $! set to <code>EINTR</code>) in places where they previously would have succeeded.</p>

<p>The default <code>:perlio</code> layer retries <code>read</code>, <code>write</code> and <code>close</code> as described above; interrupted <code>wait</code> and <code>waitpid</code> calls will always be retried.</p>

<dt><a name='Signals'></a><a name="Signals_as_&#34;faults&#34;"
>Signals as &#34;faults&#34;</a></dt>

<dd>
<p>Certain signals like SEGV, ILL, and BUS are generated by virtual memory addressing errors and similar &#34;faults&#34;. These are normally fatal: there is little a Perl-level handler can do with them. So Perl now delivers them immediately rather than attempting to defer them.</p>

<dt><a name='Signals'></a><a name="Signals_triggered_by_operating_system_state"
>Signals triggered by operating system state</a></dt>

<dd>
<p>On some operating systems certain signal handlers are supposed to &#34;do something&#34; before returning. One example can be CHLD or CLD, which indicates a child process has completed. On some operating systems the signal handler is expected to <code>wait</code> for the completed child process. On such systems the deferred signal scheme will not work for those signals: it does not do the <code>wait</code>. Again the failure will look like a loop as the operating system will reissue the signal because there are completed child processes that have not yet been <code>wait</code>ed for.</p>
</dd>
</dl>

<p>If you want the old signal behavior back despite possible memory corruption, set the environment variable <code>PERL_SIGNALS</code> to <code>&#34;unsafe&#34;</code>. This feature first appeared in Perl 5.8.1.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="Named_Pipes"
>Named Pipes <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>A named pipe (often referred to as a FIFO) is an old Unix IPC mechanism for processes communicating on the same machine. It works just like regular anonymous pipes, except that the processes rendezvous using a filename and need not be related.</p>

<p>To create a named pipe, use the <code>POSIX::mkfifo()</code> function.</p>

<pre class="sh_perl">    use POSIX qw(mkfifo);
    mkfifo($path, 0700)     ||  die &#34;mkfifo $path failed: $!&#34;;</pre>

<p>You can also use the Unix command mknod(1), or on some systems, mkfifo(1). These may not be in your normal path, though.</p>

<pre class="sh_perl">    # system return val is backwards, so &#38;&#38; not ||
    #
    $ENV{PATH} .= &#34;:/etc:/usr/etc&#34;;
    if  (      system(&#34;mknod&#34;,  $path, &#34;p&#34;)
            &#38;&#38; system(&#34;mkfifo&#34;, $path) )
    {
        die &#34;mk{nod,fifo} $path failed&#34;;
    }</pre>

<p>A fifo is convenient when you want to connect a process to an unrelated one. When you open a fifo, the program will block until there&#39;s something on the other end.</p>

<p>For example, let&#39;s say you&#39;d like to have your <em>.signature</em> file be a named pipe that has a Perl program on the other end. Now every time any program (like a mailer, news reader, finger program, etc.) tries to read from that file, the reading program will read the new signature from your program. We&#39;ll use the pipe-checking file-test operator, <b>-p</b>, to find out whether anyone (or anything) has accidentally removed our fifo.</p>

<pre class="sh_perl">    chdir();    # go home
    my $FIFO = &#34;.signature&#34;;

    while (1) {
        unless (-p $FIFO) {
            unlink $FIFO;   # discard any failure, will catch later
            require POSIX;  # delayed loading of heavy module
            POSIX::mkfifo($FIFO, 0700)
                                || die &#34;can&#39;t mkfifo $FIFO: $!&#34;;
        }

        # next line blocks till there&#39;s a reader
        open (FIFO, &#34;&#62; $FIFO&#34;)  || die &#34;can&#39;t open $FIFO: $!&#34;;
        print FIFO &#34;John Smith (smith\@host.org)\n&#34;, `fortune -s`;
        close(FIFO)             || die &#34;can&#39;t close $FIFO: $!&#34;;
        sleep 2;                # to avoid dup signals
    }</pre>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="Using_open()_for_IPC"
>Using open() for IPC <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>Perl&#39;s basic open() statement can also be used for unidirectional interprocess communication by either appending or prepending a pipe symbol to the second argument to open(). Here&#39;s how to start something up in a child process you intend to write to:</p>

<pre class="sh_perl">    open(SPOOLER, &#34;| cat -v | lpr -h 2&#62;/dev/null&#34;)
                        || die &#34;can&#39;t fork: $!&#34;;
    local $SIG{PIPE} = sub { die &#34;spooler pipe broke&#34; };
    print SPOOLER &#34;stuff\n&#34;;
    close SPOOLER       || die &#34;bad spool: $! $?&#34;;</pre>

<p>And here&#39;s how to start up a child process you intend to read from:</p>

<pre class="sh_perl">    open(STATUS, &#34;netstat -an 2&#62;&#38;1 |&#34;)
                        || die &#34;can&#39;t fork: $!&#34;;
    while (&#60;STATUS&#62;) {
        next if /^(tcp|udp)/;
        print;
    }
    close STATUS        || die &#34;bad netstat: $! $?&#34;;</pre>

<p>If one can be sure that a particular program is a Perl script expecting filenames in @ARGV, the clever programmer can write something like this:</p>

<pre class="sh_perl">    % program f1 &#34;cmd1|&#34; - f2 &#34;cmd2|&#34; f3 &#60; tmpfile</pre>

<p>and no matter which sort of shell it&#39;s called from, the Perl program will read from the file <em>f1</em>, the process <em>cmd1</em>, standard input (<em>tmpfile</em> in this case), the <em>f2</em> file, the <em>cmd2</em> command, and finally the <em>f3</em> file. Pretty nifty, eh?</p>

<p>You might notice that you could use backticks for much the same effect as opening a pipe for reading:</p>

<pre class="sh_perl">    print grep { !/^(tcp|udp)/ } `netstat -an 2&#62;&#38;1`;
    die &#34;bad netstatus ($?)&#34; if $?;</pre>

<p>While this is true on the surface, it&#39;s much more efficient to process the file one line or record at a time because then you don&#39;t have to read the whole thing into memory at once. It also gives you finer control of the whole process, letting you kill off the child process early if you&#39;d like.</p>

<p>Be careful to check the return values from both open() and close(). If you&#39;re <i>writing</i> to a pipe, you should also trap SIGPIPE. Otherwise, think of what happens when you start up a pipe to a command that doesn&#39;t exist: the open() will in all likelihood succeed (it only reflects the fork()&#39;s success), but then your output will fail--spectacularly. Perl can&#39;t know whether the command worked, because your command is actually running in a separate process whose exec() might have failed. Therefore, while readers of bogus commands return just a quick EOF, writers to bogus commands will get hit with a signal, which they&#39;d best be prepared to handle. Consider:</p>

<pre class="sh_perl">    open(FH, &#34;|bogus&#34;)      || die &#34;can&#39;t fork: $!&#34;;
    print FH &#34;bang\n&#34;;      #  neither necessary nor sufficient 
                            #  to check print retval!
    close(FH)               || die &#34;can&#39;t close: $!&#34;;</pre>

<p>The reason for not checking the return value from print() is because of pipe buffering; physical writes are delayed. That won&#39;t blow up until the close, and it will blow up with a SIGPIPE. To catch it, you could use this:</p>

<pre class="sh_perl">    $SIG{PIPE} = &#34;IGNORE&#34;;
    open(FH, &#34;|bogus&#34;)  || die &#34;can&#39;t fork: $!&#34;;
    print FH &#34;bang\n&#34;;
    close(FH)           || die &#34;can&#39;t close: status=$?&#34;;</pre>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Filehandles"
>Filehandles</a></h2>

<p>Both the main process and any child processes it forks share the same STDIN, STDOUT, and STDERR filehandles. If both processes try to access them at once, strange things can happen. You may also want to close or reopen the filehandles for the child. You can get around this by opening your pipe with open(), but on some systems this means that the child process cannot outlive the parent.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Background_Processes"
>Background Processes</a></h2>

<p>You can run a command in the background with:</p>

<pre class="sh_perl">    system(&#34;cmd &#38;&#34;);</pre>

<p>The command&#39;s STDOUT and STDERR (and possibly STDIN, depending on your shell) will be the same as the parent&#39;s. You won&#39;t need to catch SIGCHLD because of the double-fork taking place; see below for details.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Complete_Dissociation_of_Child_from_Parent"
>Complete Dissociation of Child from Parent</a></h2>

<p>In some cases (starting server processes, for instance) you&#39;ll want to completely dissociate the child process from the parent. This is often called daemonization. A well-behaved daemon will also chdir() to the root directory so it doesn&#39;t prevent unmounting the filesystem containing the directory from which it was launched, and redirect its standard file descriptors from and to <em>/dev/null</em> so that random output doesn&#39;t wind up on the user&#39;s terminal.</p>

<pre class="sh_perl">    use POSIX &#34;setsid&#34;;

    sub daemonize {
        chdir(&#34;/&#34;)                      || die &#34;can&#39;t chdir to /: $!&#34;;
        open(STDIN,  &#34;&#60; /dev/null&#34;)     || die &#34;can&#39;t read /dev/null: $!&#34;;
        open(STDOUT, &#34;&#62; /dev/null&#34;)     || die &#34;can&#39;t write to /dev/null: $!&#34;;
        defined(my $pid = fork())       || die &#34;can&#39;t fork: $!&#34;;
        exit if $pid;                   # non-zero now means I am the parent
        (setsid() != -1)                || die &#34;Can&#39;t start a new session: $!&#34; 
        open(STDERR, &#34;&#62;&#38;STDOUT&#34;)        || die &#34;can&#39;t dup stdout: $!&#34;;
    }</pre>

<p>The fork() has to come before the setsid() to ensure you aren&#39;t a process group leader; the setsid() will fail if you are. If your system doesn&#39;t have the setsid() function, open <em>/dev/tty</em> and use the <code>TIOCNOTTY</code> ioctl() on it instead. See tty(4) for details.</p>

<p>Non-Unix users should check their <code><i>Your_OS</i>::Process</code> module for other possible solutions.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Safe_Pipe_Opens"
>Safe Pipe Opens</a></h2>

<p>Another interesting approach to IPC is making your single program go multiprocess and communicate between--or even amongst--yourselves. The open() function will accept a file argument of either <code>&#34;-|&#34;</code> or <code>&#34;|-&#34;</code> to do a very interesting thing: it forks a child connected to the filehandle you&#39;ve opened. The child is running the same program as the parent. This is useful for safely opening a file when running under an assumed UID or GID, for example. If you open a pipe <i>to</i> minus, you can write to the filehandle you opened and your kid will find it in <i>his</i> STDIN. If you open a pipe <i>from</i> minus, you can read from the filehandle you opened whatever your kid writes to <i>his</i> STDOUT.</p>

<pre class="sh_perl">    use English qw[ -no_match_vars ];
    my $PRECIOUS = &#34;/path/to/some/safe/file&#34;;
    my $sleep_count;
    my $pid;

    do {
        $pid = open(KID_TO_WRITE, &#34;|-&#34;);
        unless (defined $pid) {
            warn &#34;cannot fork: $!&#34;;
            die &#34;bailing out&#34; if $sleep_count++ &#62; 6;
            sleep 10;
        }
    } until defined $pid;

    if ($pid) {                 # I am the parent 
        print KID_TO_WRITE @some_data;
        close(KID_TO_WRITE)     || warn &#34;kid exited $?&#34;;
    } else {                    # I am the child
        # drop permissions in setuid and/or setgid programs:
        ($EUID, $EGID) = ($UID, $GID);  
        open (OUTFILE, &#34;&#62; $PRECIOUS&#34;) 
                                || die &#34;can&#39;t open $PRECIOUS: $!&#34;;
        while (&#60;STDIN&#62;) {
            print OUTFILE;      # child&#39;s STDIN is parent&#39;s KID_TO_WRITE
        }
        close(OUTFILE)          || die &#34;can&#39;t close $PRECIOUS: $!&#34;;
        exit(0);                # don&#39;t forget this!!
    }</pre>

<p>Another common use for this construct is when you need to execute something without the shell&#39;s interference. With system(), it&#39;s straightforward, but you can&#39;t use a pipe open or backticks safely. That&#39;s because there&#39;s no way to stop the shell from getting its hands on your arguments. Instead, use lower-level control to call exec() directly.</p>

<p>Here&#39;s a safe backtick or pipe open for read:</p>

<pre class="sh_perl">    my $pid = open(KID_TO_READ, &#34;-|&#34;);
    defined($pid)           || die &#34;can&#39;t fork: $!&#34;;

    if ($pid) {             # parent
        while (&#60;KID_TO_READ&#62;) {
                            # do something interesting
        }
        close(KID_TO_READ)  || warn &#34;kid exited $?&#34;;

    } else {                # child
        ($EUID, $EGID) = ($UID, $GID); # suid only
        exec($program, @options, @args)
                            || die &#34;can&#39;t exec program: $!&#34;;
        # NOTREACHED
    }</pre>

<p>And here&#39;s a safe pipe open for writing:</p>

<pre class="sh_perl">    my $pid = open(KID_TO_WRITE, &#34;|-&#34;);
    defined($pid)           || die &#34;can&#39;t fork: $!&#34;;

    $SIG{PIPE} = sub { die &#34;whoops, $program pipe broke&#34; };

    if ($pid) {             # parent
        print KID_TO_WRITE @data;
        close(KID_TO_WRITE) || warn &#34;kid exited $?&#34;;

    } else {                # child
        ($EUID, $EGID) = ($UID, $GID);
        exec($program, @options, @args)
                            || die &#34;can&#39;t exec program: $!&#34;;
        # NOTREACHED
    }</pre>

<p>It is very easy to dead-lock a process using this form of open(), or indeed with any use of pipe() with multiple subprocesses. The example above is &#34;safe&#34; because it is simple and calls exec(). See <a href="#Avoiding_Pipe_Deadlocks" class="podlinkpod"
>&#34;Avoiding Pipe Deadlocks&#34;</a> for general safety principles, but there are extra gotchas with Safe Pipe Opens.</p>

<p>In particular, if you opened the pipe using <code>open FH, &#34;|-&#34;</code>, then you cannot simply use close() in the parent process to close an unwanted writer. Consider this code:</p>

<pre class="sh_perl">    my $pid = open(WRITER, &#34;|-&#34;);        # fork open a kid
    defined($pid)               || die &#34;first fork failed: $!&#34;;
    if ($pid) {
        if (my $sub_pid = fork()) {
            defined($sub_pid)   || die &#34;second fork failed: $!&#34;;
            close(WRITER)       || die &#34;couldn&#39;t close WRITER: $!&#34;;
            # now do something else...
        }
        else {
            # first write to WRITER
            # ...
            # then when finished
            close(WRITER)       || die &#34;couldn&#39;t close WRITER: $!&#34;;
            exit(0);
        }
    }
    else {
        # first do something with STDIN, then
        exit(0);
    }</pre>

<p>In the example above, the true parent does not want to write to the WRITER filehandle, so it closes it. However, because WRITER was opened using <code>open FH, &#34;|-&#34;</code>, it has a special behavior: closing it calls waitpid() (see <a href="/~flora/perl-5.14.2/pod/perlfunc.pod#waitpid" class="podlinkpod"
>&#34;waitpid&#34; in perlfunc</a>), which waits for the subprocess to exit. If the child process ends up waiting for something happening in the section marked &#34;do something else&#34;, you have deadlock.</p>

<p>This can also be a problem with intermediate subprocesses in more complicated code, which will call waitpid() on all open filehandles during global destruction--in no predictable order.</p>

<p>To solve this, you must manually use pipe(), fork(), and the form of open() which sets one file descriptor to another, as shown below:</p>

<pre class="sh_perl">    pipe(READER, WRITER)        || die &#34;pipe failed: $!&#34;;
    $pid = fork();
    defined($pid)               || die &#34;first fork failed: $!&#34;;
    if ($pid) {
        close READER;
        if (my $sub_pid = fork()) {
            defined($sub_pid)   || die &#34;first fork failed: $!&#34;;
            close(WRITER)       || die &#34;can&#39;t close WRITER: $!&#34;;
        }
        else {
            # write to WRITER...
            # ...
            # then  when finished
            close(WRITER)       || die &#34;can&#39;t close WRITER: $!&#34;;
            exit(0);
        }
        # write to WRITER...
    }
    else {
        open(STDIN, &#34;&#60;&#38;READER&#34;) || die &#34;can&#39;t reopen STDIN: $!&#34;;
        close(WRITER)           || die &#34;can&#39;t close WRITER: $!&#34;;
        # do something...
        exit(0);
    }</pre>

<p>Since Perl 5.8.0, you can also use the list form of <code>open</code> for pipes. This is preferred when you wish to avoid having the shell interpret metacharacters that may be in your command string.</p>

<p>So for example, instead of using:</p>

<pre class="sh_perl">    open(PS_PIPE, &#34;ps aux|&#34;)    || die &#34;can&#39;t open ps pipe: $!&#34;;</pre>

<p>One would use either of these:</p>

<pre class="sh_perl">    open(PS_PIPE, &#34;-|&#34;, &#34;ps&#34;, &#34;aux&#34;) 
                                || die &#34;can&#39;t open ps pipe: $!&#34;;

    @ps_args = qw[ ps aux ];
    open(PS_PIPE, &#34;-|&#34;, @ps_args)
                                || die &#34;can&#39;t open @ps_args|: $!&#34;;</pre>

<p>Because there are more than three arguments to open(), forks the ps(1) command <i>without</i> spawning a shell, and reads its standard output via the <code>PS_PIPE</code> filehandle. The corresponding syntax to <i>write</i> to command pipes is to use <code>&#34;|-&#34;</code> in place of <code>&#34;-|&#34;</code>.</p>

<p>This was admittedly a rather silly example, because you&#39;re using string literals whose content is perfectly safe. There is therefore no cause to resort to the harder-to-read, multi-argument form of pipe open(). However, whenever you cannot be assured that the program arguments are free of shell metacharacters, the fancier form of open() should be used. For example:</p>

<pre class="sh_perl">    @grep_args = (&#34;egrep&#34;, &#34;-i&#34;, $some_pattern, @many_files);
    open(GREP_PIPE, &#34;-|&#34;, @grep_args)
                        || die &#34;can&#39;t open @grep_args|: $!&#34;;</pre>

<p>Here the multi-argument form of pipe open() is preferred because the pattern and indeed even the filenames themselves might hold metacharacters.</p>

<p>Be aware that these operations are full Unix forks, which means they may not be correctly implemented on all alien systems. Additionally, these are not true multithreading. To learn more about threading, see the <em>modules</em> file mentioned below in the SEE ALSO section.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Avoiding_Pipe_Deadlocks"
>Avoiding Pipe Deadlocks</a></h2>

<p>Whenever you have more than one subprocess, you must be careful that each closes whichever half of any pipes created for interprocess communication it is not using. This is because any child process reading from the pipe and expecting an EOF will never receive it, and therefore never exit. A single process closing a pipe is not enough to close it; the last process with the pipe open must close it for it to read EOF.</p>

<p>Certain built-in Unix features help prevent this most of the time. For instance, filehandles have a &#34;close on exec&#34; flag, which is set <i>en masse</i> under control of the <code>$^F</code> variable. This is so any filehandles you didn&#39;t explicitly route to the STDIN, STDOUT or STDERR of a child <i>program</i> will be automatically closed.</p>

<p>Always explicitly and immediately call close() on the writable end of any pipe, unless that process is actually writing to it. Even if you don&#39;t explicitly call close(), Perl will still close() all filehandles during global destruction. As previously discussed, if those filehandles have been opened with Safe Pipe Open, this will result in calling waitpid(), which may again deadlock.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Bidirectional_Communication_with_Another_Process"
>Bidirectional Communication with Another Process</a></h2>

<p>While this works reasonably well for unidirectional communication, what about bidirectional communication? The most obvious approach doesn&#39;t work:</p>

<pre class="sh_perl">    # THIS DOES NOT WORK!!
    open(PROG_FOR_READING_AND_WRITING, &#34;| some program |&#34;)</pre>

<p>If you forget to <code>use warnings</code>, you&#39;ll miss out entirely on the helpful diagnostic message:</p>

<pre class="sh_perl">    Can&#39;t do bidirectional pipe at -e line 1.</pre>

<p>If you really want to, you can use the standard open2() from the <code>IPC::Open2</code> module to catch both ends. There&#39;s also an open3() in <code>IPC::Open3</code> for tridirectional I/O so you can also catch your child&#39;s STDERR, but doing so would then require an awkward select() loop and wouldn&#39;t allow you to use normal Perl input operations.</p>

<p>If you look at its source, you&#39;ll see that open2() uses low-level primitives like the pipe() and exec() syscalls to create all the connections. Although it might have been more efficient by using socketpair(), this would have been even less portable than it already is. The open2() and open3() functions are unlikely to work anywhere except on a Unix system, or at least one purporting POSIX compliance.</p>

<p>Here&#39;s an example of using open2():</p>

<pre class="sh_perl">    use FileHandle;
    use IPC::Open2;
    $pid = open2(*Reader, *Writer, &#34;cat -un&#34;);
    print Writer &#34;stuff\n&#34;;
    $got = &#60;Reader&#62;;</pre>

<p>The problem with this is that buffering is really going to ruin your day. Even though your <code>Writer</code> filehandle is auto-flushed so the process on the other end gets your data in a timely manner, you can&#39;t usually do anything to force that process to give its data to you in a similarly quick fashion. In this special case, we could actually so, because we gave <i>cat</i> a <b>-u</b> flag to make it unbuffered. But very few commands are designed to operate over pipes, so this seldom works unless you yourself wrote the program on the other end of the double-ended pipe.</p>

<p>A solution to this is to use a library which uses pseudottys to make your program behave more reasonably. This way you don&#39;t have to have control over the source code of the program you&#39;re using. The <code>Expect</code> module from CPAN also addresses this kind of thing. This module requires two other modules from CPAN, <code>IO::Pty</code> and <code>IO::Stty</code>. It sets up a pseudo terminal to interact with programs that insist on talking to the terminal device driver. If your system is supported, this may be your best bet.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Bidirectional_Communication_with_Yourself"
>Bidirectional Communication with Yourself</a></h2>

<p>If you want, you may make low-level pipe() and fork() syscalls to stitch this together by hand. This example only talks to itself, but you could reopen the appropriate handles to STDIN and STDOUT and call other processes. (The following example lacks proper error checking.)</p>

<pre class="sh_perl">    #!/usr/bin/perl -w
    # pipe1 - bidirectional communication using two pipe pairs
    #         designed for the socketpair-challenged
    use IO::Handle;               # thousands of lines just for autoflush :-(
    pipe(PARENT_RDR, CHILD_WTR);  # XXX: check failure?
    pipe(CHILD_RDR,  PARENT_WTR); # XXX: check failure?
    CHILD_WTR-&#62;autoflush(1);
    PARENT_WTR-&#62;autoflush(1);

    if ($pid = fork()) {
        close PARENT_RDR; 
        close PARENT_WTR;
        print CHILD_WTR &#34;Parent Pid $$ is sending this\n&#34;;
        chomp($line = &#60;CHILD_RDR&#62;);
        print &#34;Parent Pid $$ just read this: `$line&#39;\n&#34;;
        close CHILD_RDR; close CHILD_WTR;
        waitpid($pid, 0);
    } else {
        die &#34;cannot fork: $!&#34; unless defined $pid;
        close CHILD_RDR; 
        close CHILD_WTR;
        chomp($line = &#60;PARENT_RDR&#62;);
        print &#34;Child Pid $$ just read this: `$line&#39;\n&#34;;
        print PARENT_WTR &#34;Child Pid $$ is sending this\n&#34;;
        close PARENT_RDR; 
        close PARENT_WTR;
        exit(0);
    }</pre>

<p>But you don&#39;t actually have to make two pipe calls. If you have the socketpair() system call, it will do this all for you.</p>

<pre class="sh_perl">    #!/usr/bin/perl -w
    # pipe2 - bidirectional communication using socketpair
    #   &#34;the best ones always go both ways&#34;

    use Socket;
    use IO::Handle;  # thousands of lines just for autoflush :-(

    # We say AF_UNIX because although *_LOCAL is the
    # POSIX 1003.1g form of the constant, many machines
    # still don&#39;t have it.
    socketpair(CHILD, PARENT, AF_UNIX, SOCK_STREAM, PF_UNSPEC)
                                ||  die &#34;socketpair: $!&#34;;

    CHILD-&#62;autoflush(1);
    PARENT-&#62;autoflush(1);

    if ($pid = fork()) {
        close PARENT;
        print CHILD &#34;Parent Pid $$ is sending this\n&#34;;
        chomp($line = &#60;CHILD&#62;);
        print &#34;Parent Pid $$ just read this: `$line&#39;\n&#34;;
        close CHILD;
        waitpid($pid, 0);
    } else {
        die &#34;cannot fork: $!&#34; unless defined $pid;
        close CHILD;
        chomp($line = &#60;PARENT&#62;);
        print &#34;Child Pid $$ just read this: &#39;$line&#39;\n&#34;;
        print PARENT &#34;Child Pid $$ is sending this\n&#34;;
        close PARENT;
        exit(0);
    }</pre>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="Sockets:_Client/Server_Communication"
>Sockets: Client/Server Communication <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>While not entirely limited to Unix-derived operating systems (e.g., WinSock on PCs provides socket support, as do some VMS libraries), you might not have sockets on your system, in which case this section probably isn&#39;t going to do you much good. With sockets, you can do both virtual circuits like TCP streams and datagrams like UDP packets. You may be able to do even more depending on your system.</p>

<p>The Perl functions for dealing with sockets have the same names as the corresponding system calls in C, but their arguments tend to differ for two reasons. First, Perl filehandles work differently than C file descriptors. Second, Perl already knows the length of its strings, so you don&#39;t need to pass that information.</p>

<p>One of the major problems with ancient, antemillennial socket code in Perl was that it used hard-coded values for some of the constants, which severely hurt portability. If you ever see code that does anything like explicitly setting <code>$AF_INET = 2</code>, you know you&#39;re in for big trouble. An immeasurably superior approach is to use the <code>Socket</code> module, which more reliably grants access to the various constants and functions you&#39;ll need.</p>

<p>If you&#39;re not writing a server/client for an existing protocol like NNTP or SMTP, you should give some thought to how your server will know when the client has finished talking, and vice-versa. Most protocols are based on one-line messages and responses (so one party knows the other has finished when a &#34;\n&#34; is received) or multi-line messages and responses that end with a period on an empty line (&#34;\n.\n&#34; terminates a message/response).</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Internet_Line_Terminators"
>Internet Line Terminators</a></h2>

<p>The Internet line terminator is &#34;\015\012&#34;. Under ASCII variants of Unix, that could usually be written as &#34;\r\n&#34;, but under other systems, &#34;\r\n&#34; might at times be &#34;\015\015\012&#34;, &#34;\012\012\015&#34;, or something completely different. The standards specify writing &#34;\015\012&#34; to be conformant (be strict in what you provide), but they also recommend accepting a lone &#34;\012&#34; on input (be lenient in what you require). We haven&#39;t always been very good about that in the code in this manpage, but unless you&#39;re on a Mac from way back in its pre-Unix dark ages, you&#39;ll probably be ok.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Internet_TCP_Clients_and_Servers"
>Internet TCP Clients and Servers</a></h2>

<p>Use Internet-domain sockets when you want to do client-server communication that might extend to machines outside of your own system.</p>

<p>Here&#39;s a sample TCP client using Internet-domain sockets:</p>

<pre class="sh_perl">    #!/usr/bin/perl -w
    use strict;
    use Socket;
    my ($remote, $port, $iaddr, $paddr, $proto, $line);

    $remote  = shift || &#34;localhost&#34;;
    $port    = shift || 2345;  # random port
    if ($port =~ /\D/) { $port = getservbyname($port, &#34;tcp&#34;) }
    die &#34;No port&#34; unless $port;
    $iaddr   = inet_aton($remote)       || die &#34;no host: $remote&#34;;
    $paddr   = sockaddr_in($port, $iaddr);

    $proto   = getprotobyname(&#34;tcp&#34;);
    socket(SOCK, PF_INET, SOCK_STREAM, $proto)  || die &#34;socket: $!&#34;;
    connect(SOCK, $paddr)               || die &#34;connect: $!&#34;;
    while ($line = &#60;SOCK&#62;) {
        print $line;
    }

    close (SOCK)                        || die &#34;close: $!&#34;;
    exit(0);</pre>

<p>And here&#39;s a corresponding server to go along with it. We&#39;ll leave the address as <code>INADDR_ANY</code> so that the kernel can choose the appropriate interface on multihomed hosts. If you want sit on a particular interface (like the external side of a gateway or firewall machine), fill this in with your real address instead.</p>

<pre class="sh_perl">    #!/usr/bin/perl -Tw
    use strict;
    BEGIN { $ENV{PATH} = &#34;/usr/bin:/bin&#34; }
    use Socket;
    use Carp;
    my $EOL = &#34;\015\012&#34;;

    sub logmsg { print &#34;$0 $$: @_ at &#34;, scalar localtime(), &#34;\n&#34; }

    my $port  = shift || 2345;
    die &#34;invalid port&#34; unless if $port =~ /^ \d+ $/x;

    my $proto = getprotobyname(&#34;tcp&#34;);

    socket(Server, PF_INET, SOCK_STREAM, $proto)    || die &#34;socket: $!&#34;;
    setsockopt(Server, SOL_SOCKET, SO_REUSEADDR, pack(&#34;l&#34;, 1))    
                                                    || die &#34;setsockopt: $!&#34;;
    bind(Server, sockaddr_in($port, INADDR_ANY))    || die &#34;bind: $!&#34;;
    listen(Server, SOMAXCONN)                       || die &#34;listen: $!&#34;;

    logmsg &#34;server started on port $port&#34;;

    my $paddr;

    $SIG{CHLD} = \&#38;REAPER;

    for ( ; $paddr = accept(Client, Server); close Client) {
        my($port, $iaddr) = sockaddr_in($paddr);
        my $name = gethostbyaddr($iaddr, AF_INET);

        logmsg &#34;connection from $name [&#34;,
                inet_ntoa($iaddr), &#34;]
                at port $port&#34;;

        print Client &#34;Hello there, $name, it&#39;s now &#34;,
                        scalar localtime(), $EOL;
    }</pre>

<p>And here&#39;s a multithreaded version. It&#39;s multithreaded in that like most typical servers, it spawns (fork()s) a slave server to handle the client request so that the master server can quickly go back to service a new client.</p>

<pre class="sh_perl">    #!/usr/bin/perl -Tw
    use strict;
    BEGIN { $ENV{PATH} = &#34;/usr/bin:/bin&#34; }
    use Socket;
    use Carp;
    my $EOL = &#34;\015\012&#34;;

    sub spawn;  # forward declaration
    sub logmsg { print &#34;$0 $$: @_ at &#34;, scalar localtime(), &#34;\n&#34; }

    my $port  = shift || 2345;
    die &#34;invalid port&#34; unless if $port =~ /^ \d+ $/x;

    my $proto = getprotobyname(&#34;tcp&#34;);

    socket(Server, PF_INET, SOCK_STREAM, $proto)    || die &#34;socket: $!&#34;;
    setsockopt(Server, SOL_SOCKET, SO_REUSEADDR, pack(&#34;l&#34;, 1))         
                                                    || die &#34;setsockopt: $!&#34;;
    bind(Server, sockaddr_in($port, INADDR_ANY))    || die &#34;bind: $!&#34;;
    listen(Server, SOMAXCONN)                       || die &#34;listen: $!&#34;;

    logmsg &#34;server started on port $port&#34;;

    my $waitedpid = 0;
    my $paddr;

    use POSIX &#34;:sys_wait_h&#34;;
    use Errno;

    sub REAPER {
        local $!;   # don&#39;t let waitpid() overwrite current error
        while ((my $pid = waitpid(-1, WNOHANG)) &#62; 0 &#38;&#38; WIFEXITED($?)) {
            logmsg &#34;reaped $waitedpid&#34; . ($? ? &#34; with exit $?&#34; : &#34;&#34;);
        }
        $SIG{CHLD} = \&#38;REAPER;  # loathe SysV
    }

    $SIG{CHLD} = \&#38;REAPER;

    while (1) {
        $paddr = accept(Client, Server) || do {
            # try again if accept() returned because got a signal
            next if $!{EINTR};
            die &#34;accept: $!&#34;;
        };
        my ($port, $iaddr) = sockaddr_in($paddr);
        my $name = gethostbyaddr($iaddr, AF_INET);

        logmsg &#34;connection from $name [&#34;,
               inet_ntoa($iaddr),
               &#34;] at port $port&#34;;

        spawn sub {
            $| = 1;
            print &#34;Hello there, $name, it&#39;s now &#34;, scalar localtime(), $EOL;
            exec &#34;/usr/games/fortune&#34;       # XXX: &#34;wrong&#34; line terminators
                or confess &#34;can&#39;t exec fortune: $!&#34;;
        };
        close Client;
    }

    sub spawn {
        my $coderef = shift;

        unless (@_ == 0 &#38;&#38; $coderef &#38;&#38; ref($coderef) eq &#34;CODE&#34;) {
            confess &#34;usage: spawn CODEREF&#34;;
        }

        my $pid;
        unless (defined($pid = fork())) {
            logmsg &#34;cannot fork: $!&#34;;
            return;
        } 
        elsif ($pid) {
            logmsg &#34;begat $pid&#34;;
            return; # I&#39;m the parent
        }
        # else I&#39;m the child -- go spawn

        open(STDIN,  &#34;&#60;&#38;Client&#34;)    || die &#34;can&#39;t dup client to stdin&#34;;
        open(STDOUT, &#34;&#62;&#38;Client&#34;)    || die &#34;can&#39;t dup client to stdout&#34;;
        ## open(STDERR, &#34;&#62;&#38;STDOUT&#34;) || die &#34;can&#39;t dup stdout to stderr&#34;;
        exit($coderef-&#62;());
    }</pre>

<p>This server takes the trouble to clone off a child version via fork() for each incoming request. That way it can handle many requests at once, which you might not always want. Even if you don&#39;t fork(), the listen() will allow that many pending connections. Forking servers have to be particularly careful about cleaning up their dead children (called &#34;zombies&#34; in Unix parlance), because otherwise you&#39;ll quickly fill up your process table. The REAPER subroutine is used here to call waitpid() for any child processes that have finished, thereby ensuring that they terminate cleanly and don&#39;t join the ranks of the living dead.</p>

<p>Within the while loop we call accept() and check to see if it returns a false value. This would normally indicate a system error needs to be reported. However, the introduction of safe signals (see <a href="#Deferred_Signals_(Safe_Signals)" class="podlinkpod"
>&#34;Deferred Signals (Safe Signals)&#34;</a> above) in Perl 5.7.3 means that accept() might also be interrupted when the process receives a signal. This typically happens when one of the forked subprocesses exits and notifies the parent process with a CHLD signal.</p>

<p>If accept() is interrupted by a signal, $! will be set to EINTR. If this happens, we can safely continue to the next iteration of the loop and another call to accept(). It is important that your signal handling code not modify the value of $!, or else this test will likely fail. In the REAPER subroutine we create a local version of $! before calling waitpid(). When waitpid() sets $! to ECHILD as it inevitably does when it has no more children waiting, it updates the local copy and leaves the original unchanged.</p>

<p>You should use the <b>-T</b> flag to enable taint checking (see <a href="/~flora/perl-5.14.2/pod/perlsec.pod" class="podlinkpod"
>perlsec</a>) even if we aren&#39;t running setuid or setgid. This is always a good idea for servers or any program run on behalf of someone else (like CGI scripts), because it lessens the chances that people from the outside will be able to compromise your system.</p>

<p>Let&#39;s look at another TCP client. This one connects to the TCP &#34;time&#34; service on a number of different machines and shows how far their clocks differ from the system on which it&#39;s being run:</p>

<pre class="sh_perl">    #!/usr/bin/perl  -w
    use strict;
    use Socket;

    my $SECS_OF_70_YEARS = 2208988800;
    sub ctime { scalar localtime(shift() || time()) }

    my $iaddr = gethostbyname(&#34;localhost&#34;);
    my $proto = getprotobyname(&#34;tcp&#34;);
    my $port = getservbyname(&#34;time&#34;, &#34;tcp&#34;);
    my $paddr = sockaddr_in(0, $iaddr);
    my($host);

    $| = 1;
    printf &#34;%-24s %8s %s\n&#34;, &#34;localhost&#34;, 0, ctime();

    foreach $host (@ARGV) {
        printf &#34;%-24s &#34;, $host;
        my $hisiaddr = inet_aton($host)     || die &#34;unknown host&#34;;
        my $hispaddr = sockaddr_in($port, $hisiaddr);
        socket(SOCKET, PF_INET, SOCK_STREAM, $proto)   
                                            || die &#34;socket: $!&#34;;
        connect(SOCKET, $hispaddr)          || die &#34;connect: $!&#34;;
        my $rtime = pack(&#34;C4&#34;, ());
        read(SOCKET, $rtime, 4);
        close(SOCKET);
        my $histime = unpack(&#34;N&#34;, $rtime) - $SECS_OF_70_YEARS;
        printf &#34;%8d %s\n&#34;, $histime - time(), ctime($histime);
    }</pre>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Unix-Domain_TCP_Clients_and_Servers"
>Unix-Domain TCP Clients and Servers</a></h2>

<p>That&#39;s fine for Internet-domain clients and servers, but what about local communications? While you can use the same setup, sometimes you don&#39;t want to. Unix-domain sockets are local to the current host, and are often used internally to implement pipes. Unlike Internet domain sockets, Unix domain sockets can show up in the file system with an ls(1) listing.</p>

<pre class="sh_perl">    % ls -l /dev/log
    srw-rw-rw-  1 root            0 Oct 31 07:23 /dev/log</pre>

<p>You can test for these with Perl&#39;s <b>-S</b> file test:</p>

<pre class="sh_perl">    unless (-S &#34;/dev/log&#34;) {
        die &#34;something&#39;s wicked with the log system&#34;;
    }</pre>

<p>Here&#39;s a sample Unix-domain client:</p>

<pre class="sh_perl">    #!/usr/bin/perl -w
    use Socket;
    use strict;
    my ($rendezvous, $line);

    $rendezvous = shift || &#34;catsock&#34;;
    socket(SOCK, PF_UNIX, SOCK_STREAM, 0)     || die &#34;socket: $!&#34;;
    connect(SOCK, sockaddr_un($rendezvous))   || die &#34;connect: $!&#34;;
    while (defined($line = &#60;SOCK&#62;)) {
        print $line;
    }
    exit(0);</pre>

<p>And here&#39;s a corresponding server. You don&#39;t have to worry about silly network terminators here because Unix domain sockets are guaranteed to be on the localhost, and thus everything works right.</p>

<pre class="sh_perl">    #!/usr/bin/perl -Tw
    use strict;
    use Socket;
    use Carp;

    BEGIN { $ENV{PATH} = &#34;/usr/bin:/bin&#34; }
    sub spawn;  # forward declaration
    sub logmsg { print &#34;$0 $$: @_ at &#34;, scalar localtime(), &#34;\n&#34; }

    my $NAME = &#34;catsock&#34;;
    my $uaddr = sockaddr_un($NAME);
    my $proto = getprotobyname(&#34;tcp&#34;);

    socket(Server, PF_UNIX, SOCK_STREAM, 0) || die &#34;socket: $!&#34;;
    unlink($NAME);
    bind  (Server, $uaddr)                  || die &#34;bind: $!&#34;;
    listen(Server, SOMAXCONN)               || die &#34;listen: $!&#34;;

    logmsg &#34;server started on $NAME&#34;;

    my $waitedpid;

    use POSIX &#34;:sys_wait_h&#34;;
    sub REAPER {
        my $child;
        while (($waitedpid = waitpid(-1, WNOHANG)) &#62; 0) {
            logmsg &#34;reaped $waitedpid&#34; . ($? ? &#34; with exit $?&#34; : &#34;&#34;);
        }
        $SIG{CHLD} = \&#38;REAPER;  # loathe SysV
    }

    $SIG{CHLD} = \&#38;REAPER;


    for ( $waitedpid = 0;
          accept(Client, Server) || $waitedpid;
          $waitedpid = 0, close Client)
    {
        next if $waitedpid;
        logmsg &#34;connection on $NAME&#34;;
        spawn sub {
            print &#34;Hello there, it&#39;s now &#34;, scalar localtime(), &#34;\n&#34;;
            exec(&#34;/usr/games/fortune&#34;)  || die &#34;can&#39;t exec fortune: $!&#34;;
        };
    }

    sub spawn {
        my $coderef = shift();

        unless (@_ == 0 &#38;&#38; $coderef &#38;&#38; ref($coderef) eq &#34;CODE&#34;) {
            confess &#34;usage: spawn CODEREF&#34;;
        }

        my $pid;
        unless (defined($pid = fork())) {
            logmsg &#34;cannot fork: $!&#34;;
            return;
        } 
        elsif ($pid) {
            logmsg &#34;begat $pid&#34;;
            return; # I&#39;m the parent
        } 
        else {
            # I&#39;m the child -- go spawn
        }

        open(STDIN,  &#34;&#60;&#38;Client&#34;)    || die &#34;can&#39;t dup client to stdin&#34;;
        open(STDOUT, &#34;&#62;&#38;Client&#34;)    || die &#34;can&#39;t dup client to stdout&#34;;
        ## open(STDERR, &#34;&#62;&#38;STDOUT&#34;) || die &#34;can&#39;t dup stdout to stderr&#34;;
        exit($coderef-&#62;());
    }</pre>

<p>As you see, it&#39;s remarkably similar to the Internet domain TCP server, so much so, in fact, that we&#39;ve omitted several duplicate functions--spawn(), logmsg(), ctime(), and REAPER()--which are the same as in the other server.</p>

<p>So why would you ever want to use a Unix domain socket instead of a simpler named pipe? Because a named pipe doesn&#39;t give you sessions. You can&#39;t tell one process&#39;s data from another&#39;s. With socket programming, you get a separate session for each client; that&#39;s why accept() takes two arguments.</p>

<p>For example, let&#39;s say that you have a long-running database server daemon that you want folks to be able to access from the Web, but only if they go through a CGI interface. You&#39;d have a small, simple CGI program that does whatever checks and logging you feel like, and then acts as a Unix-domain client and connects to your private server.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="TCP_Clients_with_IO::Socket"
>TCP Clients with IO::Socket <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>For those preferring a higher-level interface to socket programming, the IO::Socket module provides an object-oriented approach. IO::Socket has been included in the standard Perl distribution ever since Perl 5.004. If you&#39;re running an earlier version of Perl (in which case, how are you reading this manpage?), just fetch IO::Socket from CPAN, where you&#39;ll also find modules providing easy interfaces to the following systems: DNS, FTP, Ident (RFC 931), NIS and NISPlus, NNTP, Ping, POP3, SMTP, SNMP, SSLeay, Telnet, and Time--to name just a few.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="A_Simple_Client"
>A Simple Client</a></h2>

<p>Here&#39;s a client that creates a TCP connection to the &#34;daytime&#34; service at port 13 of the host name &#34;localhost&#34; and prints out everything that the server there cares to provide.</p>

<pre class="sh_perl">    #!/usr/bin/perl -w
    use IO::Socket;
    $remote = IO::Socket::INET-&#62;new(
                        Proto    =&#62; &#34;tcp&#34;,
                        PeerAddr =&#62; &#34;localhost&#34;,
                        PeerPort =&#62; &#34;daytime(13)&#34;,
                    )
                  || die &#34;can&#39;t connect to daytime service on localhost&#34;;
    while (&#60;$remote&#62;) { print }</pre>

<p>When you run this program, you should get something back that looks like this:</p>

<pre class="sh_perl">    Wed May 14 08:40:46 MDT 1997</pre>

<p>Here are what those parameters to the new() constructor mean:</p>

<dl>
<dt><a name="Proto"
><code>Proto</code></a></dt>

<dd>
<p>This is which protocol to use. In this case, the socket handle returned will be connected to a TCP socket, because we want a stream-oriented connection, that is, one that acts pretty much like a plain old file. Not all sockets are this of this type. For example, the UDP protocol can be used to make a datagram socket, used for message-passing.</p>

<dt><a name="PeerAddr"
><code>PeerAddr</code></a></dt>

<dd>
<p>This is the name or Internet address of the remote host the server is running on. We could have specified a longer name like <code>&#34;www.perl.com&#34;</code>, or an address like <code>&#34;207.171.7.72&#34;</code>. For demonstration purposes, we&#39;ve used the special hostname <code>&#34;localhost&#34;</code>, which should always mean the current machine you&#39;re running on. The corresponding Internet address for localhost is <code>&#34;127.0.0.1&#34;</code>, if you&#39;d rather use that.</p>

<dt><a name="PeerPort"
><code>PeerPort</code></a></dt>

<dd>
<p>This is the service name or port number we&#39;d like to connect to. We could have gotten away with using just <code>&#34;daytime&#34;</code> on systems with a well-configured system services file,[FOOTNOTE: The system services file is found in <i>/etc/services</i> under Unixy systems.] but here we&#39;ve specified the port number (13) in parentheses. Using just the number would have also worked, but numeric literals make careful programmers nervous.</p>
</dd>
</dl>

<p>Notice how the return value from the <code>new</code> constructor is used as a filehandle in the <code>while</code> loop? That&#39;s what&#39;s called an <i>indirect filehandle</i>, a scalar variable containing a filehandle. You can use it the same way you would a normal filehandle. For example, you can read one line from it this way:</p>

<pre class="sh_perl">    $line = &#60;$handle&#62;;</pre>

<p>all remaining lines from is this way:</p>

<pre class="sh_perl">    @lines = &#60;$handle&#62;;</pre>

<p>and send a line of data to it this way:</p>

<pre class="sh_perl">    print $handle &#34;some data\n&#34;;</pre>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="A_Webget_Client"
>A Webget Client</a></h2>

<p>Here&#39;s a simple client that takes a remote host to fetch a document from, and then a list of files to get from that host. This is a more interesting client than the previous one because it first sends something to the server before fetching the server&#39;s response.</p>

<pre class="sh_perl">    #!/usr/bin/perl -w
    use IO::Socket;
    unless (@ARGV &#62; 1) { die &#34;usage: $0 host url ...&#34; }
    $host = shift(@ARGV);
    $EOL = &#34;\015\012&#34;;
    $BLANK = $EOL x 2;
    for my $document (@ARGV) {
        $remote = IO::Socket::INET-&#62;new( Proto     =&#62; &#34;tcp&#34;,
                                         PeerAddr  =&#62; $host,
                                         PeerPort  =&#62; &#34;http(80)&#34;,
                  )     || die &#34;cannot connect to httpd on $host&#34;;
        $remote-&#62;autoflush(1);
        print $remote &#34;GET $document HTTP/1.0&#34; . $BLANK;
        while ( &#60;$remote&#62; ) { print }
        close $remote;
    }</pre>

<p>The web server handling the HTTP service is assumed to be at its standard port, number 80. If the server you&#39;re trying to connect to is at a different port, like 1080 or 8080, you should specify it as the named-parameter pair, <code>PeerPort =&#62; 8080</code>. The <code>autoflush</code> method is used on the socket because otherwise the system would buffer up the output we sent it. (If you&#39;re on a prehistoric Mac, you&#39;ll also need to change every <code>&#34;\n&#34;</code> in your code that sends data over the network to be a <code>&#34;\015\012&#34;</code> instead.)</p>

<p>Connecting to the server is only the first part of the process: once you have the connection, you have to use the server&#39;s language. Each server on the network has its own little command language that it expects as input. The string that we send to the server starting with &#34;GET&#34; is in HTTP syntax. In this case, we simply request each specified document. Yes, we really are making a new connection for each document, even though it&#39;s the same host. That&#39;s the way you always used to have to speak HTTP. Recent versions of web browsers may request that the remote server leave the connection open a little while, but the server doesn&#39;t have to honor such a request.</p>

<p>Here&#39;s an example of running that program, which we&#39;ll call <i>webget</i>:</p>

<pre class="sh_perl">    % webget www.perl.com /guanaco.html
    HTTP/1.1 404 File Not Found
    Date: Thu, 08 May 1997 18:02:32 GMT
    Server: Apache/1.2b6
    Connection: close
    Content-type: text/html

    &#60;HEAD&#62;&#60;TITLE&#62;404 File Not Found&#60;/TITLE&#62;&#60;/HEAD&#62;
    &#60;BODY&#62;&#60;H1&#62;File Not Found&#60;/H1&#62;
    The requested URL /guanaco.html was not found on this server.&#60;P&#62;
    &#60;/BODY&#62;</pre>

<p>Ok, so that&#39;s not very interesting, because it didn&#39;t find that particular document. But a long response wouldn&#39;t have fit on this page.</p>

<p>For a more featureful version of this program, you should look to the <i>lwp-request</i> program included with the LWP modules from CPAN.</p>

<h2><a class='u' href='#___top' title='click to go to top of document'
name="Interactive_Client_with_IO::Socket"
>Interactive Client with IO::Socket</a></h2>

<p>Well, that&#39;s all fine if you want to send one command and get one answer, but what about setting up something fully interactive, somewhat like the way <i>telnet</i> works? That way you can type a line, get the answer, type a line, get the answer, etc.</p>

<p>This client is more complicated than the two we&#39;ve done so far, but if you&#39;re on a system that supports the powerful <code>fork</code> call, the solution isn&#39;t that rough. Once you&#39;ve made the connection to whatever service you&#39;d like to chat with, call <code>fork</code> to clone your process. Each of these two identical process has a very simple job to do: the parent copies everything from the socket to standard output, while the child simultaneously copies everything from standard input to the socket. To accomplish the same thing using just one process would be <i>much</i> harder, because it&#39;s easier to code two processes to do one thing than it is to code one process to do two things. (This keep-it-simple principle a cornerstones of the Unix philosophy, and good software engineering as well, which is probably why it&#39;s spread to other systems.)</p>

<p>Here&#39;s the code:</p>

<pre class="sh_perl">    #!/usr/bin/perl -w
    use strict;
    use IO::Socket;
    my ($host, $port, $kidpid, $handle, $line);

    unless (@ARGV == 2) { die &#34;usage: $0 host port&#34; }
    ($host, $port) = @ARGV;

    # create a tcp connection to the specified host and port
    $handle = IO::Socket::INET-&#62;new(Proto     =&#62; &#34;tcp&#34;,
                                    PeerAddr  =&#62; $host,
                                    PeerPort  =&#62; $port)
               || die &#34;can&#39;t connect to port $port on $host: $!&#34;;

    $handle-&#62;autoflush(1);       # so output gets there right away
    print STDERR &#34;[Connected to $host:$port]\n&#34;;

    # split the program into two processes, identical twins
    die &#34;can&#39;t fork: $!&#34; unless defined($kidpid = fork());

    # the if{} block runs only in the parent process
    if ($kidpid) {
        # copy the socket to standard output
        while (defined ($line = &#60;$handle&#62;)) {
            print STDOUT $line;
        }
        kill(&#34;TERM&#34;, $kidpid);   # send SIGTERM to child
    }
    # the else{} block runs only in the child process
    else {
        # copy standard input to the socket
        while (defined ($line = &#60;STDIN&#62;)) {
            print $handle $line;
        }
        exit(0);                # just in case
    }</pre>

<p>The <code>kill</code> function in the parent&#39;s <code>if</code> block is there to send a signal to our child process, currently running in the <code>else</code> block, as soon as the remote server has closed its end of the connection.</p>

<p>If the remote server sends data a byte at time, and you need that data immediately without waiting for a newline (which might not happen), you may wish to replace the <code>while</code> loop in the parent with the following:</p>

<pre class="sh_perl">    my $byte;
    while (sysread($handle, $byte, 1) == 1) {
        print STDOUT $byte;
    }</pre>

<p>Making a system call for each byte you want to read is not very efficient (to put it mildly) but is the simplest to explain and works reasonably well.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="TCP_Servers_with_IO::Socket"
>TCP Servers with IO::Socket <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>As always, setting up a server is little bit more involved than running a client. The model is that the server creates a special kind of socket that does nothing but listen on a particular port for incoming connections. It does this by calling the <code>IO::Socket::INET-&#62;new()</code> method with slightly different arguments than the client did.</p>

<dl>
<dt><a name="Proto"
>Proto</a></dt>

<dd>
<p>This is which protocol to use. Like our clients, we&#39;ll still specify <code>&#34;tcp&#34;</code> here.</p>

<dt><a name="LocalPort"
>LocalPort</a></dt>

<dd>
<p>We specify a local port in the <code>LocalPort</code> argument, which we didn&#39;t do for the client. This is service name or port number for which you want to be the server. (Under Unix, ports under 1024 are restricted to the superuser.) In our sample, we&#39;ll use port 9000, but you can use any port that&#39;s not currently in use on your system. If you try to use one already in used, you&#39;ll get an &#34;Address already in use&#34; message. Under Unix, the <code>netstat -a</code> command will show which services current have servers.</p>

<dt><a name="Listen"
>Listen</a></dt>

<dd>
<p>The <code>Listen</code> parameter is set to the maximum number of pending connections we can accept until we turn away incoming clients. Think of it as a call-waiting queue for your telephone. The low-level Socket module has a special symbol for the system maximum, which is SOMAXCONN.</p>

<dt><a name="Reuse"
>Reuse</a></dt>

<dd>
<p>The <code>Reuse</code> parameter is needed so that we restart our server manually without waiting a few minutes to allow system buffers to clear out.</p>
</dd>
</dl>

<p>Once the generic server socket has been created using the parameters listed above, the server then waits for a new client to connect to it. The server blocks in the <code>accept</code> method, which eventually accepts a bidirectional connection from the remote client. (Make sure to autoflush this handle to circumvent buffering.)</p>

<p>To add to user-friendliness, our server prompts the user for commands. Most servers don&#39;t do this. Because of the prompt without a newline, you&#39;ll have to use the <code>sysread</code> variant of the interactive client above.</p>

<p>This server accepts one of five different commands, sending output back to the client. Unlike most network servers, this one handles only one incoming client at a time. Multithreaded servers are covered in Chapter 16 of the Camel.</p>

<p>Here&#39;s the code. We&#39;ll</p>

<pre class="sh_perl"> #!/usr/bin/perl -w
 use IO::Socket;
 use Net::hostent;      # for OOish version of gethostbyaddr

 $PORT = 9000;          # pick something not in use

 $server = IO::Socket::INET-&#62;new( Proto     =&#62; &#34;tcp&#34;,
                                  LocalPort =&#62; $PORT,
                                  Listen    =&#62; SOMAXCONN,
                                  Reuse     =&#62; 1);

 die &#34;can&#39;t setup server&#34; unless $server;
 print &#34;[Server $0 accepting clients]\n&#34;;

 while ($client = $server-&#62;accept()) {
   $client-&#62;autoflush(1);
   print $client &#34;Welcome to $0; type help for command list.\n&#34;;
   $hostinfo = gethostbyaddr($client-&#62;peeraddr);
   printf &#34;[Connect from %s]\n&#34;, $hostinfo ? $hostinfo-&#62;name : $client-&#62;peerhost;
   print $client &#34;Command? &#34;;
   while ( &#60;$client&#62;) {
     next unless /\S/;       # blank line
     if    (/quit|exit/i)    { last                                      }
     elsif (/date|time/i)    { printf $client &#34;%s\n&#34;, scalar localtime() }
     elsif (/who/i )         { print  $client `who 2&#62;&#38;1`                 }
     elsif (/cookie/i )      { print  $client `/usr/games/fortune 2&#62;&#38;1`  }
     elsif (/motd/i )        { print  $client `cat /etc/motd 2&#62;&#38;1`       }
     else {
       print $client &#34;Commands: quit date who cookie motd\n&#34;;
     }
   } continue {
      print $client &#34;Command? &#34;;
   }
   close $client;
 }</pre>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="UDP:_Message_Passing"
>UDP: Message Passing <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>Another kind of client-server setup is one that uses not connections, but messages. UDP communications involve much lower overhead but also provide less reliability, as there are no promises that messages will arrive at all, let alone in order and unmangled. Still, UDP offers some advantages over TCP, including being able to &#34;broadcast&#34; or &#34;multicast&#34; to a whole bunch of destination hosts at once (usually on your local subnet). If you find yourself overly concerned about reliability and start building checks into your message system, then you probably should use just TCP to start with.</p>

<p>UDP datagrams are <i>not</i> a bytestream and should not be treated as such. This makes using I/O mechanisms with internal buffering like stdio (i.e. print() and friends) especially cumbersome. Use syswrite(), or better send(), like in the example below.</p>

<p>Here&#39;s a UDP program similar to the sample Internet TCP client given earlier. However, instead of checking one host at a time, the UDP version will check many of them asynchronously by simulating a multicast and then using select() to do a timed-out wait for I/O. To do something similar with TCP, you&#39;d have to use a different socket handle for each host.</p>

<pre class="sh_perl">    #!/usr/bin/perl -w
    use strict;
    use Socket;
    use Sys::Hostname;

    my ( $count, $hisiaddr, $hispaddr, $histime,
         $host, $iaddr, $paddr, $port, $proto,
         $rin, $rout, $rtime, $SECS_OF_70_YEARS);

    $SECS_OF_70_YEARS = 2_208_988_800;

    $iaddr = gethostbyname(hostname());
    $proto = getprotobyname(&#34;udp&#34;);
    $port = getservbyname(&#34;time&#34;, &#34;udp&#34;);
    $paddr = sockaddr_in(0, $iaddr); # 0 means let kernel pick

    socket(SOCKET, PF_INET, SOCK_DGRAM, $proto)   || die &#34;socket: $!&#34;;
    bind(SOCKET, $paddr)                          || die &#34;bind: $!&#34;;

    $| = 1;
    printf &#34;%-12s %8s %s\n&#34;,  &#34;localhost&#34;, 0, scalar localtime();
    $count = 0;
    for $host (@ARGV) {
        $count++;
        $hisiaddr = inet_aton($host)              || die &#34;unknown host&#34;;
        $hispaddr = sockaddr_in($port, $hisiaddr);
        defined(send(SOCKET, 0, 0, $hispaddr))    || die &#34;send $host: $!&#34;;
    }

    $rin = &#34;&#34;;
    vec($rin, fileno(SOCKET), 1) = 1;

    # timeout after 10.0 seconds
    while ($count &#38;&#38; select($rout = $rin, undef, undef, 10.0)) {
        $rtime = &#34;&#34;;
        $hispaddr = recv(SOCKET, $rtime, 4, 0)    || die &#34;recv: $!&#34;;
        ($port, $hisiaddr) = sockaddr_in($hispaddr);
        $host = gethostbyaddr($hisiaddr, AF_INET);
        $histime = unpack(&#34;N&#34;, $rtime) - $SECS_OF_70_YEARS;
        printf &#34;%-12s &#34;, $host;
        printf &#34;%8d %s\n&#34;, $histime - time(), scalar localtime($histime);
        $count--;
    }</pre>

<p>This example does not include any retries and may consequently fail to contact a reachable host. The most prominent reason for this is congestion of the queues on the sending host if the number of hosts to contact is sufficiently large.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="SysV_IPC"
>SysV IPC <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>While System V IPC isn&#39;t so widely used as sockets, it still has some interesting uses. However, you cannot use SysV IPC or Berkeley mmap() to have a variable shared amongst several processes. That&#39;s because Perl would reallocate your string when you weren&#39;t wanting it to. You might look into the <code>IPC::Shareable</code> or <code>threads::shared</code> modules for that.</p>

<p>Here&#39;s a small example showing shared memory usage.</p>

<pre class="sh_perl">    use IPC::SysV qw(IPC_PRIVATE IPC_RMID S_IRUSR S_IWUSR);

    $size = 2000;
    $id = shmget(IPC_PRIVATE, $size, S_IRUSR | S_IWUSR);
    defined($id)                    || die &#34;shmget: $!&#34;;
    print &#34;shm key $id\n&#34;;

    $message = &#34;Message #1&#34;;
    shmwrite($id, $message, 0, 60)  || die &#34;shmwrite: $!&#34;;
    print &#34;wrote: &#39;$message&#39;\n&#34;;
    shmread($id, $buff, 0, 60)      || die &#34;shmread: $!&#34;;
    print &#34;read : &#39;$buff&#39;\n&#34;;

    # the buffer of shmread is zero-character end-padded.
    substr($buff, index($buff, &#34;\0&#34;)) = &#34;&#34;:
    print &#34;un&#34; unless $buff eq $message;
    print &#34;swell\n&#34;;

    print &#34;deleting shm $id\n&#34;;
    shmctl($id, IPC_RMID, 0)        || die &#34;shmctl: $!&#34;;</pre>

<p>Here&#39;s an example of a semaphore:</p>

<pre class="sh_perl">    use IPC::SysV qw(IPC_CREAT);

    $IPC_KEY = 1234;
    $id = semget($IPC_KEY, 10, 0666 | IPC_CREAT);
    defined($id)                    || die &#34;shmget: $!&#34;;
    print &#34;shm key $id\n&#34;;</pre>

<p>Put this code in a separate file to be run in more than one process. Call the file <em>take</em>:</p>

<pre class="sh_perl">    # create a semaphore

    $IPC_KEY = 1234;
    $id = semget($IPC_KEY, 0, 0);
    defined($id)                    || die &#34;shmget: $!&#34;;

    $semnum  = 0;
    $semflag = 0;

    # &#34;take&#34; semaphore
    # wait for semaphore to be zero
    $semop = 0;
    $opstring1 = pack(&#34;s!s!s!&#34;, $semnum, $semop, $semflag);

    # Increment the semaphore count
    $semop = 1;
    $opstring2 = pack(&#34;s!s!s!&#34;, $semnum, $semop,  $semflag);
    $opstring  = $opstring1 . $opstring2;

    semop($id, $opstring)   || die &#34;semop: $!&#34;;</pre>

<p>Put this code in a separate file to be run in more than one process. Call this file <em>give</em>:</p>

<pre class="sh_perl">    # &#34;give&#34; the semaphore
    # run this in the original process and you will see
    # that the second process continues

    $IPC_KEY = 1234;
    $id = semget($IPC_KEY, 0, 0);
    die unless defined($id);

    $semnum  = 0;
    $semflag = 0;

    # Decrement the semaphore count
    $semop = -1;
    $opstring = pack(&#34;s!s!s!&#34;, $semnum, $semop, $semflag);

    semop($id, $opstring)   || die &#34;semop: $!&#34;;</pre>

<p>The SysV IPC code above was written long ago, and it&#39;s definitely clunky looking. For a more modern look, see the IPC::SysV module which is included with Perl starting from Perl 5.005.</p>

<p>A small example demonstrating SysV message queues:</p>

<pre class="sh_perl">    use IPC::SysV qw(IPC_PRIVATE IPC_RMID IPC_CREAT S_IRUSR S_IWUSR);

    my $id = msgget(IPC_PRIVATE, IPC_CREAT | S_IRUSR | S_IWUSR);
    defined($id)                || die &#34;msgget failed: $!&#34;;

    my $sent      = &#34;message&#34;;
    my $type_sent = 1234;

    msgsnd($id, pack(&#34;l! a*&#34;, $type_sent, $sent), 0)
                                || die &#34;msgsnd failed: $!&#34;;

    msgrcv($id, my $rcvd_buf, 60, 0, 0)
                                || die &#34;msgrcv failed: $!&#34;;

    my($type_rcvd, $rcvd) = unpack(&#34;l! a*&#34;, $rcvd_buf);

    if ($rcvd eq $sent) {
        print &#34;okay\n&#34;;
    } else {
        print &#34;not okay\n&#34;;
    }

    msgctl($id, IPC_RMID, 0)    || die &#34;msgctl failed: $!\n&#34;;</pre>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="NOTES"
>NOTES <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>Most of these routines quietly but politely return <code>undef</code> when they fail instead of causing your program to die right then and there due to an uncaught exception. (Actually, some of the new <i>Socket</i> conversion functions do croak() on bad arguments.) It is therefore essential to check return values from these functions. Always begin your socket programs this way for optimal success, and don&#39;t forget to add the <b>-T</b> taint-checking flag to the <code>#!</code> line for servers:</p>

<pre class="sh_perl">    #!/usr/bin/perl -Tw
    use strict;
    use sigtrap;
    use Socket;</pre>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="BUGS"
>BUGS <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>These routines all create system-specific portability problems. As noted elsewhere, Perl is at the mercy of your C libraries for much of its system behavior. It&#39;s probably safest to assume broken SysV semantics for signals and to stick with simple TCP and UDP socket operations; e.g., don&#39;t try to pass open file descriptors over a local UDP datagram socket if you want your code to stand a chance of being portable.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="AUTHOR"
>AUTHOR <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>Tom Christiansen, with occasional vestiges of Larry Wall&#39;s original version and suggestions from the Perl Porters.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="SEE_ALSO"
>SEE ALSO <img alt='^' src='http://st.pimg.net/tucs/img/up.gif'></a></h1>

<p>There&#39;s a lot more to networking than this, but this should get you started.</p>

<p>For intrepid programmers, the indispensable textbook is <i>Unix Network Programming, 2nd Edition, Volume 1</i> by W. Richard Stevens (published by Prentice-Hall). Most books on networking address the subject from the perspective of a C programmer; translation to Perl is left as an exercise for the reader.</p>

<p>The <a href="/perldoc?IO%3A%3ASocket" class="podlinkpod"
>IO::Socket</a>(3) manpage describes the object library, and the Socket(3) manpage describes the low-level interface to sockets. Besides the obvious functions in <a href="/~flora/perl-5.14.2/pod/perlfunc.pod" class="podlinkpod"
>perlfunc</a>, you should also check out the <em>modules</em> file at your nearest CPAN site, especially <a href="http://www.cpan.org/modules/00modlist.long.html#ID5_Networking_" class="podlinkurl"
>http://www.cpan.org/modules/00modlist.long.html#ID5_Networking_</a>. See <a href="/~flora/perl-5.14.2/pod/perlmodlib.PL" class="podlinkpod"
>perlmodlib</a> or best yet, the <em>Perl FAQ</em> for a description of what CPAN is and where to get it if the previous link doesn&#39;t work for you.</p>

<p>Section 5 of CPAN&#39;s <em>modules</em> file is devoted to &#34;Networking, Device Control (modems), and Interprocess Communication&#34;, and contains numerous unbundled modules numerous networking modules, Chat and Expect operations, CGI programming, DCE, FTP, IPC, NNTP, Proxy, Ptty, RPC, SNMP, SMTP, Telnet, Threads, and ToolTalk--to name just a few.</p>

</div>
<!-- This should probably be put in the <div class="footer"></div> -->
<script type="text/javascript">
    $(document).ready(function(){
        var startingStyle = $.cookie('css') ? $.cookie('css') : 'http://st.pimg.net/tucs/css/sh_none.min.css';
        $.fn.styleSwitch(startingStyle);
        $("#styleswitch").val(startingStyle);
        sh_highlightDocument();
        $("#styleswitch").bind(($.browser.msie ? "click" : "change"), function() {
            $.fn.styleSwitch($(this).val());
        });
    });
</script>
<div class="styleswitch">
    syntax highlighting:
    <select id="styleswitch">
        <option value="http://st.pimg.net/tucs/css/sh_none.min.css">no syntax highlighting</option>
        <option value="http://st.pimg.net/tucs/css/sh_acid.min.css">acid</option>
        <option value="http://st.pimg.net/tucs/css/sh_berries-dark.min.css">berries-dark</option>
        <option value="http://st.pimg.net/tucs/css/sh_berries-light.min.css">berries-light</option>
        <option value="http://st.pimg.net/tucs/css/sh_bipolar.min.css">bipolar</option>
        <option value="http://st.pimg.net/tucs/css/sh_blacknblue.min.css">blacknblue</option>
        <option value="http://st.pimg.net/tucs/css/sh_bright.min.css">bright</option>
        <option value="http://st.pimg.net/tucs/css/sh_contrast.min.css">contrast</option>
        <option value="http://st.pimg.net/tucs/css/sh_cpan.min.css">cpan</option>
        <option value="http://st.pimg.net/tucs/css/sh_darkblue.min.css">darkblue</option>
        <option value="http://st.pimg.net/tucs/css/sh_darkness.min.css">darkness</option>
        <option value="http://st.pimg.net/tucs/css/sh_desert.min.css">desert</option>
        <option value="http://st.pimg.net/tucs/css/sh_dull.min.css">dull</option>
        <option value="http://st.pimg.net/tucs/css/sh_easter.min.css">easter</option>
        <option value="http://st.pimg.net/tucs/css/sh_emacs.min.css">emacs</option>
        <option value="http://st.pimg.net/tucs/css/sh_golden.min.css">golden</option>
        <option value="http://st.pimg.net/tucs/css/sh_greenlcd.min.css">greenlcd</option>
        <option value="http://st.pimg.net/tucs/css/sh_ide-anjuta.min.css">ide-anjuta</option>
        <option value="http://st.pimg.net/tucs/css/sh_ide-codewarrior.min.css">ide-codewarrior</option>
        <option value="http://st.pimg.net/tucs/css/sh_ide-devcpp.min.css">ide-devcpp</option>
        <option value="http://st.pimg.net/tucs/css/sh_ide-eclipse.min.css">ide-eclipse</option>
        <option value="http://st.pimg.net/tucs/css/sh_ide-kdev.min.css">ide-kdev</option>
        <option value="http://st.pimg.net/tucs/css/sh_ide-msvcpp.min.css">ide-msvcpp</option>
        <option value="http://st.pimg.net/tucs/css/sh_kwrite.min.css">kwrite</option>
        <option value="http://st.pimg.net/tucs/css/sh_matlab.min.css">matlab</option>
        <option value="http://st.pimg.net/tucs/css/sh_navy.min.css">navy</option>
        <option value="http://st.pimg.net/tucs/css/sh_nedit.min.css">nedit</option>
        <option value="http://st.pimg.net/tucs/css/sh_neon.min.css">neon</option>
        <option value="http://st.pimg.net/tucs/css/sh_night.min.css">night</option>
        <option value="http://st.pimg.net/tucs/css/sh_pablo.min.css">pablo</option>
        <option value="http://st.pimg.net/tucs/css/sh_peachpuff.min.css">peachpuff</option>
        <option value="http://st.pimg.net/tucs/css/sh_print.min.css">print</option>
        <option value="http://st.pimg.net/tucs/css/sh_rand01.min.css">rand01</option>
	<option value="http://st.pimg.net/tucs/css/sh_solarized-dark.min.css">solarized-dark</option>
	<option value="http://st.pimg.net/tucs/css/sh_solarized-light.min.css">solarized-light</option>
        <option value="http://st.pimg.net/tucs/css/sh_style.min.css">style</option>
        <option value="http://st.pimg.net/tucs/css/sh_the.min.css">the</option>
        <option value="http://st.pimg.net/tucs/css/sh_typical.min.css">typical</option>
        <option value="http://st.pimg.net/tucs/css/sh_vampire.min.css">vampire</option>
        <option value="http://st.pimg.net/tucs/css/sh_vim-dark.min.css">vim-dark</option>
        <option value="http://st.pimg.net/tucs/css/sh_vim.min.css">vim</option>
        <option value="http://st.pimg.net/tucs/css/sh_whatis.min.css">whatis</option>
        <option value="http://st.pimg.net/tucs/css/sh_whitengrey.min.css">whitengrey</option>
        <option value="http://st.pimg.net/tucs/css/sh_zellner.min.css">zellner</option>
    </select>
</div>



<div class="footer"><div class="cpanstats">78231 Uploads, 24723 Distributions
106029 Modules, 9649 Uploaders
</div>
hosted by <a href="http://www.yellowbot.com">YellowBot</a><br/>
<a href="http://www.yellowbot.com"><img alt="do. tag. write. share." src="http://st.pimg.net/tucs/img/yellowbot_logo.gif"></a>
</div>
<script type="text/javascript">
var gaJsHost = (("https:" == document.location.protocol) ? "https://ssl." : "http://www.");
document.write(unescape("%3Cscript src='" + gaJsHost + "google-analytics.com/ga.js' type='text/javascript'%3E%3C/script%3E"));
</script>
<script type="text/javascript" src="http://ipv4.v6test.develooper.com/js/v1/v6test.js"></script>

<script type="text/javascript">
   // v6.target = '';
   if (!v6.target) { v6.only_once = true }
   v6.site = '7A0D89A6-2B82-11DF-B9DA-F61CBD13F020';
   v6.api_server = 'http://ipv4.v6test.develooper.com';
   try {
     v6.test();
   } catch(err) {}
</script>
<script type="text/javascript">
  $(document).ready(function(){
    $("a[href^=http:]").click(function(){
      var href = $(this).attr('href');
      var m = href.match('\/\/([^\/:]+)');
      _gaq.push(['_trackEvent','External',m[1],'Document']);
    });
    $("a[href^=/CPAN/]").click(function(){
      var href = $(this).attr('href');
      _gaq.push(['_trackEvent','Download',href,'Document']);
    });
  });
</script>
<!-- Thu Apr 26 22:23:46 2012 GMT (0.231868982315063) @cpansearch1 -->
 </body>
</html>
